POKY_DISTRO ??= "poky"
require conf/distro/${POKY_DISTRO}.conf

DISTRO = "ovc-mini"
DISTRO_NAME = "ovc-mini (Ka-Ro Linux BSP with OVC flavoring)"
DISTRO_VERSION = "5.10-${DISTRO_CODENAME}"

POKY_DEFAULT_DISTRO_FEATURES_remove = " \
        largefile \
        opengl \
        ptest \
        vulkan \
        wayland \
"

DISTRO_FEATURES_DEFAULT_remove = " \
        3g \
        argp \
        bluetooth \
        nfc \
        pcmcia \
        wifi \
        x11 \
        zeroconf \
"

DISTRO_FEATURES_append = " \
        alternatives-symlinks-relative \
        dhcpcd \
        ldconfig \
        pam \
        telnet-login \
        iwlwifi \
"

MACHINE_FIRMWARE_append_mx8 = "\
    linux-firmware-iwlwifi \
"

DISTRO_FEATURES_append_qsxm = " csi-camera pcie tc358867 wifi intel-wifi"
DISTRO_FEATURES_append_qsxp = " csi-camera pcie tc358867 wifi intel-wifi"

VIRTUAL-RUNTIME_base-utils-hwclock = "util-linux-hwclock"
VIRTUAL-RUNTIME_dev_manager ?= "udev"
VIRTUAL-RUNTIME_init_manager ?= "${@bb.utils.contains('DISTRO_FEATURES',"systemd","systemd","sysvinit",d)}"

PREFERRED_PROVIDER_opencl-headers = "imx-gpu-viv"

VOLATILE_LOG_DIR = "no"

# mask out sysvinit recipes from meta-imx layer
BBMASK += ".*/meta-imx/.*/sysvinit/.*"

# mask out bbappend from devil since it's fetch gives 404
BBMASK += ".*/meta-imx/meta-sdk/recipes-graphics/devil/devil_%.bbappend"

# mask out recipe breaking the weston desktop without systemd
BBMASK += ".*/meta-imx/meta-bsp/recipes-graphics/wayland/weston-init.bbappend"

BBMASK += ".*/meta-karo-nxp-distro/conf/distro/.*"
BBMASK += ".*/meta-karo-nxp/conf/machine/.*"
